/-
Copyright 2025 The Formal Conjectures Authors.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    https://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
-/

import FormalConjectures.Util.ProblemImports

/-!
# ErdÅ‘s Problem 1

*Reference:* [erdosproblems.com/1](https://www.erdosproblems.com/1)
-/

open Filter

open scoped Topology Real

/--
A finite set of naturals $A$ is said to be a sum-distinct set for $N \in \mathbb{N}$ if
$A\subseteq\{1, ..., N\}$ and the sums  $\sum_{a\in S}a$ are distinct for all $S\subseteq A$
-/
abbrev IsSumDistinctSet (A : Finset â„•) (N : â„•) : Prop :=
    A âŠ† Finset.Icc 1 N âˆ§ (fun (âŸ¨S, _âŸ© : A.powerset) => S.sum id).Injective

/--
If $A\subseteq\{1, ..., N\}$ with $|A| = n$ is such that the subset sums $\sum_{a\in S}a$ are
distinct for all $S\subseteq A$ then
$$
  N \gg 2 ^ n.
$$
-/
@[category research open, AMS 5 11]
theorem erdos_1 : âˆƒ C > (0 : â„), âˆ€ (N : â„•) (A : Finset â„•) (_ : IsSumDistinctSet A N),
    N â‰  0 â†’ C * 2 ^ A.card < N := by
  sorry

/--
The trivial lower bound is $N \gg 2^n / n$.
-/
@[category undergraduate, AMS 5 11]
theorem erdos_1.variants.weaker : âˆƒ C > (0 : â„), âˆ€ (N : â„•) (A : Finset â„•)
    (_ : IsSumDistinctSet A N), N â‰  0 â†’ C * 2 ^ A.card / A.card < N := by
  sorry

/--
ErdÅ‘s and Moser [Er56] proved
$$
  N \geq (\tfrac{1}{4} - o(1)) \frac{2^n}{\sqrt{n}}.
$$

[Er56] ErdÅ‘s, P., _Problems and results in additive number theory_. Colloque sur la Th\'{E}orie des Nombres, Bruxelles, 1955 (1956), 127-137.
-/
@[category research solved, AMS 5 11]
theorem erdos_1.variants.lb : âˆƒ (o : â„• â†’ â„) (_ : Tendsto o atTop (ğ“ 0)),
    âˆ€ (N : â„•) (A : Finset â„•) (h : IsSumDistinctSet A N),
      (1 / 4 - o A.card) * 2 ^ A.card / (A.card : â„).sqrt â‰¤ N := by
  sorry

/--
A number of improvements of the constant $\frac{1}{4}$ have been given, with the current
record $\sqrt{2 / \pi}$ first provied in unpublished work of Elkies and Gleason.
-/
@[category research solved, AMS 5 11]
theorem erdos_1.variants.lb_strong : âˆƒ (o : â„• â†’ â„) (_ : Tendsto o atTop (ğ“ 0)),
    âˆ€ (N : â„•) (A : Finset â„•) (h : IsSumDistinctSet A N),
      (âˆš(2 / Ï€) - o A.card) * 2 ^ A.card / (A.card : â„).sqrt â‰¤ N := by
  sorry

/--
A finite set of real numbers is said to be sum-distinct if all the subset sums differ by
at least $1$.
-/
abbrev IsSumDistinctRealSet (A : Finset â„) (N : â„•) : Prop :=
    A.toSet âŠ† Set.Ioc 0 N âˆ§ A.powerset.toSet.Pairwise fun Sâ‚ Sâ‚‚ => 1 â‰¤ dist (Sâ‚.sum id) (Sâ‚‚.sum id)

/--
A generalisation of the problem to sets $A \subseteq (0, N]$ of real numbers, such that the subset
sums all differ by at least $1$ is proposed in [Er73] and [ErGr80].

[Er73] ErdÅ‘s, P., _Problems and results on combinatorial number theory_. A survey of combinatorial theory (Proc. Internat. Sympos., Colorado State Univ., Fort Collins, Colo., 1971) (1973), 117-138.

[ErGr80] ErdÅ‘s, P. and Graham, R., _Old and new problems and results in combinatorial number theory_. Monographies de L'Enseignement Mathematique (1980).
-/
@[category research open, AMS 5 11]
theorem erdos_1.variants.real : âˆƒ C > (0 : â„), âˆ€ (N : â„•) (A : Finset â„)
    (_ : IsSumDistinctRealSet A N), N â‰  0 â†’ C * 2 ^ A.card < N := by
  sorry

/--
The minimal value of $N$ such that there exists a sum-distinct set with three
elements is $4$.

https://oeis.org/A276661
-/
@[category undergraduate, AMS 5 11]
theorem erdos_1.variants.least_N_3 :
    IsLeast { N | âˆƒ A, IsSumDistinctSet A N âˆ§ A.card = 3 } 4 := by
  refine âŸ¨âŸ¨{1, 2, 4}, ?_âŸ©, ?_âŸ©
  Â· simp
    refine âŸ¨by decide, ?_âŸ©
    let P := Finset.powerset {1, 2, 4}
    have : Finset.univ.image (fun p : P â†¦ âˆ‘ x âˆˆ p, x) = {0, 1, 2, 4, 3, 5, 6, 7} := by
      refine Finset.ext_iff.mpr (fun n => ?_)
      simp [show P = {{}, {1}, {2}, {4}, {1, 2}, {1, 4}, {2, 4}, {1, 2, 4}} by decide]
      omega
    rw [Set.injective_iff_injOn_univ, â† Finset.coe_univ]
    have : (Finset.univ.image (fun p : P â†¦ âˆ‘ x âˆˆ p.1, x)).card = (Finset.univ (Î± := P)).card := by
      rw [this]; aesop
    exact Finset.injOn_of_card_image_eq this
  Â· simp [mem_lowerBounds]
    intro n S h h_inj hcard3
    by_contra hn
    interval_cases n; aesop; aesop
    Â· have := Finset.card_le_card h
      aesop
    Â· absurd h_inj
      rw [(Finset.subset_iff_eq_of_card_le (Nat.le_of_eq (by rw [hcard3]; decide))).mp h]
      decide

/--
The minimal value of $N$ such that there exists a sum-distinct set with five
elements is $13$.

https://oeis.org/A276661
-/
@[category research solved, AMS 5 11]
theorem erdos_1.variants.least_N_5 :
    IsLeast { N | âˆƒ A, IsSumDistinctSet A N âˆ§ A.card = 5 } 13 := by
  sorry

/--
The minimal value of $N$ such that there exists a sum-distinct set with nine
elements is $161$.

https://oeis.org/A276661
-/
@[category research solved, AMS 5 11]
theorem erdos_1.variants.least_N_9 :
    IsLeast { N | âˆƒ A, IsSumDistinctSet A N âˆ§ A.card = 9 } 161 := by
  sorry
